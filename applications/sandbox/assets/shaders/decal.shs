#version 450
#state DEPTH OFF
#state CULL FRONT

generate(vertex, fragment)

#include<stdio.shinc>
#include<math_ext.shinc>
#include <lighting.shinc>

shader(vertex)
{
    void main(void)
    {
        gl_Position = ModelToScreenSpacePosition(position);
        PropagateStdIO();
    }
}

shader(fragment)
{
    uniform vec4 skycolor;

    void main(void)
    {
        // Get the world position from the gbuffer.
        vec3 worldScenePos = ScenePosition();

        // If the worldspace was 0 there's either nothing (skybox) or something that doesn't want to me messed with.
        if(worldScenePos == vec3(0.0))
            discard;

        // Transform to local space.
        vec3 localScenePos = (inverse(stdin(modelMatrix)) * vec4(worldScenePos.xyz, 1.0)).xyz;

        // If the read position is outside of this projection shape then it's not part of the decal.
        if(max(abs(localScenePos.x), max(abs(localScenePos.y), abs(localScenePos.z))) > 0.5)
            discard;

        // Get camera for lighting.
        Camera camera = GetCamera();

        // Get the world normal from the gbuffer
        vec3 worldNormal = SceneNormal();

        // If the worldNormal is 0 then something doesn't like to be messed with.
        if(worldNormal == vec3(0.0))
            discard;

        // Calculate world tangent.
        vec3 worldTangent = (stdin(modelMatrix) * vec4(1, 0, 0, 1)).xyz;
        worldTangent = normalize(worldTangent - dot(worldTangent, worldNormal) * worldNormal);

        // Get material parameters from uv, camera, world normal and world tangent.
        Material material = ExtractMaterial(localScenePos.xz + vec2(0.5), camera, worldNormal, worldTangent);

        // Get all lighting for this fragment.
        vec3 lighting = GetAllLighting(material, camera, worldScenePos);

        // Do skybox lighting hack.
        vec3 unitDir = reflect(-normalize(camera.toView), material.normal);
        float reflectance = clamp01(0.01 + (pow(max(0.0, (1.0 - material.roughness)), 4) * 0.99));
        vec3 reflection = mix(mix(vec3(average(skycolor.rgb)), skycolor.rgb, clamp01(dot(unitDir, vec3(0.f, 1.f, 0.f)) + 0.9)), (vec3(average(skycolor.rgb)) + skycolor.rgb)*0.5, material.roughness);

        lighting += reflection * mix(vec3(1.0), material.albedo.rgb, material.metallic) * reflectance;

        // Submit output values to gbuffers.
        SubmitOutput(vec4(lighting, material.albedo.a), worldNormal, worldScenePos, material.emissive);
    }
}
